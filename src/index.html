<!doctype html>
<html lang="en">
  <head>
    <meta charset="UTF-8" />
    <title>dreamseqs visuals</title>
    <style>
      html {
        margin: 0;
        padding: 0;
      }

      body {
        margin: 0;
      }

      canvas {
        display: block;
        width: 100vw;
        height: 100vh;
        aspect-ratio: 16 / 9;
        object-fit: contain;
        background-color: #000000;
      }
    </style>

    <script id="vertex-shader" type="x-shader/x-vertex">
      #version 300 es

      in vec2 position;
      uniform uint currentTimeMs;
      uniform vec2 resolution;
      flat out uint vCurrentTimeMs;
      out vec2 vResolution;

      void main() {
        vCurrentTimeMs = currentTimeMs;
        vResolution = resolution;
        gl_Position = vec4(position, 0.0, 1.0);
      }
    </script>

    <script id="fragment-shader-base">
      #version 300 es

      precision highp float;
      flat in uint vCurrentTimeMs;
      in vec2 vResolution;
      out vec4 outColor;

      const float PI = 3.14159265;

      // @see https://github.com/dmnsgn/glsl-rotate/blob/main/rotation-3d.glsl
      // @see https://wgld.org/d/glsl/g017.html
      vec3 rotate(vec3 position, float angleRadian, vec3 axis) {
        axis = normalize(axis);
        float s = sin(angleRadian);
        float c = cos(angleRadian);
        float oc = 1.0 - c;

        mat3 rotateMatrix = mat3(
          oc * axis.x * axis.x + c,           oc * axis.x * axis.y - axis.z * s,  oc * axis.z * axis.x + axis.y * s,
          oc * axis.x * axis.y + axis.z * s,  oc * axis.y * axis.y + c,           oc * axis.y * axis.z - axis.x * s,
          oc * axis.z * axis.x - axis.y * s,  oc * axis.y * axis.z + axis.x * s,  oc * axis.z * axis.z + c
        );

        return rotateMatrix * position;
      }

      vec3 rotateXDegree(vec3 position, float angleDegree) {
        return rotate(position, radians(angleDegree), vec3(1.0, 0.0, 0.0));
      }

      vec3 rotateYDegree(vec3 position, float angleDegree) {
        return rotate(position, radians(angleDegree), vec3(0.0, 1.0, 0.0));
      }

      vec3 rotateZDegree(vec3 position, float angleDegree) {
        return rotate(position, radians(angleDegree), vec3(0.0, 0.0, 1.0));
      }

      // @see https://stackoverflow.com/questions/4200224/random-noise-functions-for-glsl
      float pseudoRandom(vec2 co) {
        return fract(sin(dot(co.xy, vec2(12.9898, 78.233))) * 43758.5453);
      }
    </script>

    <script id="fragment-shader-01" type="x-shader/x-fragment">
      const vec3 lightDirection = normalize(vec3(0.0, 0.0, -1.0));

      float distanceFunc(vec3 position) {
        return length(mod(position, 4.0) - 2.0) - 1.0;
      }

      vec3 normalFunc(vec3 position) {
        float delta = 0.001;
        return normalize(vec3(
          distanceFunc(position + vec3(delta, 0.0, 0.0)) - distanceFunc(position - vec3(delta, 0.0, 0.0)),
          distanceFunc(position + vec3(0.0, delta, 0.0)) - distanceFunc(position - vec3(0.0, delta, 0.0)),
          distanceFunc(position + vec3(0.0, 0.0, delta)) - distanceFunc(position - vec3(0.0, 0.0, delta))
        ));
      }

      void main() {
        vec2 fragmentPosition = (gl_FragCoord.xy * 2.0 - vResolution) / vResolution;

        float progress = float(vCurrentTimeMs % uint(16000)) / 16000.0;

        vec3 cameraPosition = vec3(0.0, -3.0 + 32.0 * progress, 0.0);

        vec3 ray = normalize(vec3(
          cos(PI * (-fragmentPosition.x + 0.5)) * cos(PI * fragmentPosition.y * 0.5),
          sin(PI * (-fragmentPosition.x + 0.5)) * cos(PI * fragmentPosition.y * 0.5),
          sin(PI * fragmentPosition.y * 0.5)
        ));

        ray = rotateYDegree(ray, 360.0 * progress);

        float distance = 0.0;
        float rayLength = 0.0;
        vec3 rayPosition = cameraPosition;

        for (int i = 1; i < 32; i++) {
          distance = distanceFunc(rayPosition);
          rayLength = rayLength + distance;
          rayPosition = cameraPosition + ray * rayLength;
        }

        float brightness = min((10.0 - rayLength) / 5.0, 1.0) * step(rayLength, 10.0) * step(distance, 0.001) * clamp(dot(lightDirection, normalFunc(rayPosition)), 0.1, 1.0);

        outColor = vec4(
          brightness,
          brightness,
          brightness,
          1.0
        );
      }
    </script>

    <script src="./visual_base.js"></script>
    <script>
      window.addEventListener('load', () => {
        const vertexShaderSourceCode = document.getElementById("vertex-shader").text;
        const fragmentShaderSourceCodeBase = document.getElementById("fragment-shader-base").text;
        const fragmentShaderSourceCode01 = document.getElementById("fragment-shader-01").text;
        const fragmentShaderSourceCode = (fragmentShaderSourceCodeBase + fragmentShaderSourceCode01);
        const visual = new Visual();
        visual.setShaderSourceCode(vertexShaderSourceCode.trim(), fragmentShaderSourceCode.trim());
        visual.run();
      });
    </script>
  </head>
  <body>
    <canvas class="canvas" id="canvas" width="1920" height="1080"></canvas>
  </body>
</html>
